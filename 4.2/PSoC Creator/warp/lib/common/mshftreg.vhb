--

--
-- MSHIFTREG
--
library cypress;
use cypress.rtlpkg.cy_dsrff;
use cypress.lpmpkg.all;
entity mshiftreg is
    generic(lpm_width : positive := 1;
     lpm_direction : shdir_type:= LPM_LEFT;
     lpm_avalue : bit_vector := "";
     lpm_svalue : bit_vector := "";
     lpm_pvalue : bit_vector := "";
     lpm_hint : goal_type := SPEED);
    port (data : in bit_vector(lpm_width-1 downto 0) := (others => zero);
     clock : in bit;
     enable : in bit := one;
     shiftin : in bit := zero;
     load : in bit := zero;
     q : out bit_vector(lpm_width-1 downto 0);
     shiftout : out bit;
     aset : in bit := zero;
     aclr : in bit := zero;
     sset : in bit := zero;
     sclr : in bit := zero;
     testenab : in bit := zero;
     testin : in bit := zero;
     testout : out bit);
end mshiftreg;

architecture archlpmbit of Mshiftreg is

    signal qtmp,d,set,clr : bit_vector(q'high downto 0);
    signal async,sync : bit_vector(q'high downto 0);

begin

    async <= restring(lpm_avalue,lpm_width);
    sync <= restring(lpm_svalue,lpm_width);

    -- Determine the value of the Input to the register
    process (data,sync,sset,sclr,load,enable,shiftin,testin,testenab,qtmp)
    begin
        if (testenab = '1') then
            d(0) <= testin;
            if (lpm_width > 1) then
                d(q'high downto 1) <= qtmp((qtmp'high - 1) downto 0);
            end if;
        elsif (sclr = '1' AND enable = '1') then
            d <= (others => '0');
        elsif (sset = '1' AND enable = '1') then
            d <= sync;
        elsif (load = '1' AND enable = '1') then
            d <= data;
        elsif (enable = '1') then
            if (lpm_direction = lpm_left) then
                d(0) <= shiftin;
                if (lpm_width > 1) then
                    d(q'high downto 1) <= qtmp((qtmp'high - 1) downto 0);
                end if;
            else
                d(q'high) <= shiftin;
                if (lpm_width > 1) then
                    d(q'high - 1 downto 0) <= qtmp(qtmp'high downto 1);
                end if;
            end if;
        else
            d <= qtmp;
        end if;
    end process;

    G0: for j in q'range generate
        set(j) <= (aset AND async(j));
        clr(j) <= (aset AND NOT async(j)) OR aclr;

        UJ:cy_dsrff port map (d(j), set(j), clr(j), clock, qtmp(j));
    end generate;

    shiftout <= qtmp(qtmp'high) when (lpm_direction = lpm_left) else
                qtmp(qtmp'low);
    testout <= qtmp(qtmp'high);
    q <= qtmp;

end archlpmbit;
